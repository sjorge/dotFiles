### theme - dual
## Double line theme
##
## Support variables:
## PROMPT_HIGHLIGHT  :: color of hostname if local      (gray)
## PROMPT_REMOTE     :: color of hostname if remote     (yellow)
## PROMPT_CO         :: char when command exits with 0  (.)
## PROMPT_CE         :: char when command exits with >0 (!)

## prompt
PROMPT=$'%{$fg_bold[grey]%}[%{$fg_bold[$(prompt_dual_helper_hostname)]%}%m %{$fg_bold[grey]%}:: %(!.%{$fg_bold[red]%}.%{$fg_bold[green]%})%n%{$fg_bold[grey]%}][%{$fg_bold[white]%}%~%{$fg_bold[grey]%}]$(prompt_dual_helper_git_info)\n%{$fg_bold[grey]%}[%(?,%{$fg_bold[green]%}${PROMPT_CO:-.}%{$reset_color%},%{$fg_bold[red]%}${PROMPT_CE:-!}%{$reset_color%})%{$fg_bold[grey]%}]%(!.%{$fg_bold[red]%}#.%{$fg_bold[green]%}$(prompt_dual_helper_vimode))%{$reset_color%} '
RPROMPT=''

## hostname helper
prompt_dual_helper_hostname() {
  if [[ -n "${SSH_CONNECTION}" ]] then
		echo -n "${PROMPT_REMOTE:-yellow}"
  else
		echo -n "${PROMPT_HIGHLIGHT:-grey}"
  fi
}

## vi-mode helper
prompt_dual_helper_vimode() {
  if [[ $KEYMAP = vicmd ]]; then
    echo "#"
  else
    echo "$"
  fi
}

## git helpers
# NOTE: taken mostly from oh-my-zsh
prompt_dual_helper_git_info() {
  local ref
	ref=$(command git symbolic-ref HEAD 2> /dev/null) || \
	ref=$(command git rev-parse --short HEAD 2> /dev/null) || return 0
	echo "%{$fg_bold[grey]%}[%{$reset_color%}${ref#refs/heads/}$(prompt_dual_helper_git_dirty)%{$fg_bold[grey]%}]%{$reset_color%}"
}
prompt_dual_helper_git_dirty() {
  git_compare_version() {
    local INPUT_GIT_VERSION INSTALLED_GIT_VERSION
    INPUT_GIT_VERSION=(${(s/./)1})
    INSTALLED_GIT_VERSION=($(command git --version 2>/dev/null))
    INSTALLED_GIT_VERSION=(${(s/./)INSTALLED_GIT_VERSION[3]})

    for i in {1..3}; do
      if [[ $INSTALLED_GIT_VERSION[$i] -gt $INPUT_GIT_VERSION[$i] ]]; then
        echo 1
        return 0
      fi
      if [[ $INSTALLED_GIT_VERSION[$i] -lt $INPUT_GIT_VERSION[$i] ]]; then
        echo -1
        return 0
      fi
    done
    echo 0
  }

  local STATUS=''
  local FLAGS
  FLAGS=('--porcelain')
	if [[ "$(git_compare_version "1.7.2")" -gt 0 ]]; then
		FLAGS+='--ignore-submodules=dirty'
	fi
	if [[ "$DISABLE_UNTRACKED_FILES_DIRTY" == "true" ]]; then
		FLAGS+='--untracked-files=no'
	fi
	STATUS=$(command git status ${FLAGS} 2> /dev/null | tail -n1)
  if [[ -n $STATUS ]]; then
    if [[ "${LANG}" =~ "UTF-8$" ]]; then
      echo " %{$fg_bold[red]%}✗%{$reset_color%}"
    else
      echo " %{$fg_bold[red]%}*%{$reset_color%}"
    fi
  else
    if [[ "${LANG}" =~ "UTF-8$" ]]; then
      echo " %{$fg_bold[green]%}✔%{$reset_color%}"
    fi
  fi
}


# vim: tabstop=2 expandtab shiftwidth=2 softtabstop=2

