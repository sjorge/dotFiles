#### cmdlet for copying to the clipboard
## clipcopy - Copy data to clipboard
## https://github.com/robbyrussell/oh-my-zsh/blob/master/lib/clipboard.zsh
##
## Usage:
##  <command> | clipcopy    - copies stdin to clipboard
##  clipcopy <file>         - copies a file's contents to clipboard

function clipcopy() {
  emulate -L zsh
  local file=$1
  if [[ $OSTYPE == darwin* ]]; then
    if [[ -z $file ]]; then
      pbcopy
    else
      cat $file | pbcopy
    fi
  elif [[ $OSTYPE == cygwin* ]]; then
    if [[ -z $file ]]; then
      cat > /dev/clipboard
    else
      cat $file > /dev/clipboard
    fi
  else
    if (( $+commands[xclip] )); then
      if [[ -z $file ]]; then
        xclip -in -selection clipboard
      else
        xclip -in -selection clipboard $file
      fi
    elif (( $+commands[xsel] )); then
      if [[ -z $file ]]; then
        xsel --clipboard --input 
      else
        cat "$file" | xsel --clipboard --input
      fi
    else
      print "clipcopy: Platform $OSTYPE not supported or xclip/xsel not installed" >&2
      return 1
    fi
  fi
}

# vim: tabstop=2 expandtab shiftwidth=2 softtabstop=2
